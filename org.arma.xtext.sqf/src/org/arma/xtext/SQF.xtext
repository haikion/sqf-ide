/*
 * SQF programming language syntax model
 */

grammar org.arma.xtext.Sqf hidden(WS, SL_COMMENT, ML_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.xtext.org/arma/Config" as Config

generate sqf "http://www.xtext.org/arma/Sqf"

//SQF consists of lines of code. Each line of code
//is separated by semicolumn. Trailing semicolumn is optional
//Allow empty code eg. {}
Model: {Model}
	lines+=LineOfCode? (';' lines+=LineOfCode)* ';'?
;

//Do not validate this
LineMiddleSpecial:
	COMMAND_MIDDLE_SPECIAL parameters=VariableValue
;

//Validate this
LineMiddle:
	name=UNRECOGNIZED_STRING parameters=VariableValue
;

//Return value of this code line can be assigned to a variable
//eg. _var = [1,2,3,4] select 2 select 3;
AssignableLineOfCode:
	frontParameter=VariableValue middles+=(LineMiddle|LineMiddleSpecial)*
;

LineOfCode:
	AssignableLineOfCode
	|LocalVariableDefinition
	|valu=GlobalVariableDefinition
	|Include
	|If|While|For|ForVariable|Switch
;

//Command definitions (terminal cannot be used as case-insensitivity doesn't work with it).

//Contains both CommandLeft and Variable definition
//Look-a-head to counter LL. Might cause performance issues because LineOfCode
//can be huge... 
CommandLeft:
	name=GlobalVariableReference parameter=VariableValue
;

GlobalVariableReference:
	name=[Config::Variable|UNRECOGNIZED_STRING]
;

CommandLeftSpecial:
	COMMAND_LEFT_SPECIAL parameter=VariableValue
;

//Variable parser rules
Code: 
	'{'Model'}'
;

Countable:
	IntV
	|FloatV
	|SpecialVariable
	|CommandParameterless
	|CommandLeftSpecial	
	|LocalVariableReference
	|'(' Model ')'	
	//CommandLeftGeneral always contains command and its parameter
	//separated by a white space
	|=>CommandLeft
	|GlobalVariableReference
;

Comparable:
	StringV|Countable
;

NonArray: Comparable|Code;

//Contains whatever can be assigned to a variable.
VariableValue: NonArray|Array|GlobalFunction; 

Array:
	{Array} '['(member += LineOfCode (',' member += LineOfCode)*)?']'
;

ForVariable:
	'for' StringV 'from' from=Countable 'to' to=Countable ('step' step=Countable)?
	'do' loopCode=Code
;

//Special case of CommandMiddle
LocalVariableDefinition:
	name=LOCAL_VARIABLE ASSIGMENT_OPERATOR value=LineOfCode
;

GlobalVariableDefinition returns Config::Variable:
	name=(UNRECOGNIZED_STRING|GLOBAL_FUNCTION) ASSIGMENT_OPERATOR val=LineOfCode
;

//TODO: 
//1) Separate so that global applies to whole project
//   and local applies to open file
//2) Figure out how to handle blocks so that local only effects
//   current code block
LocalVariableReference:
	name=[LocalVariableDefinition|LOCAL_VARIABLE]
;

For:
	'for' Array
	'do' loopCode=Code
;

If: //COMMAND_LEFT_SPECIAL == !
	'if' COMMAND_LEFT_SPECIAL? '(' condition=LineOfCode ')' ('then'|'exitWith') thenCode=Code 
	('else' elseCode=Code)?
;

While:
	'while' condition=Code 'do' 
	loopCode=Code
;
//"switch (playerSide) do { case west: {}; }", "Switch"
SwitchBlock: {SwitchBlock}
	'{' ('case' cases+=LineOfCode ':' blocks+=Code ';')* ('case'?'default' blocks+=Code ';')? '}'
;

Switch:
	'switch' '(' var=LineOfCode ')' 'do' block=SwitchBlock
;

//Dummy rules for terminal->Parser rule conversion.
//Enables auto complete. Mixing terminal and parser rules
//is not a good practice?
SpecialVariable:
	name=SPECIAL_VARIABLE
;

GlobalFunction:
    name=GLOBAL_FUNCTION
;

LocalVariable:
	name=LOCAL_VARIABLE
;

IntV:
	INT
;

FloatV:
	name=FLOAT
;

StringV:
	name=STRING_TERMINAL
;

Include:
	'#include' importURI=STRING_TERMINAL
;

//FIXME: Allow underscored tags. Difficulty: Very hard due to lack of look-a-head in the Lexer.
//Work-a-round atm: Global Variable References containing _fnc_ are by-passed.
terminal GLOBAL_FUNCTION :
	('a'..'z'|'A'..'Z'|'0'..'9')+'_'('f'|'F')('n'|'N')('c'|'C')'_'('a'..'z'|'A'..'Z'|'_'|'0'..'9')+
;
terminal SPECIAL_VARIABLE: '_this'|'_x'|'this'|'p1';
terminal LOCAL_VARIABLE : '_'('a'..'z'|'A'..'Z'|'_'|'0'..'9')+;

//Non character cases of command middle
terminal COMMAND_MIDDLE_SPECIAL: '>>'|'=='|'!='|'<'|'>'|'>='|'<='|'+'|'-'
	|'*'|'/'|'^'|'||'|'&&'|'%'
;

//Non character cases of command left
terminal COMMAND_LEFT_SPECIAL: '!';
/*
terminal COMMAND_L_M_SPECIAL:
	'call' //Solves the problem with global variable reference being identified as a left command...
	|'hint'
;
*/

//Variable definitons
terminal FLOAT: INT '.' UNSIGNED;
terminal INT: '-'? UNSIGNED;
terminal UNSIGNED: ('0'..'9')+;
terminal DOUBLE_QUETO: '""';
terminal STRING_TERMINAL	: 
			'"' (DOUBLE_QUETO | !'"')* '"' 
			| "'" !"'"* "'"
		;

//Ignore
//terminal BRACKET_CONTENT: '(' -> ')'; //No syntax checking inside brackets
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS	: (' '|'\t'|'\r'|'\n')+;

//terminal ASSIGMENT_OPERATOR: !('='|'!'|'>'|'<')'='!('='); //Single =, rules out: ==,!=,>=,<=
terminal ASSIGMENT_OPERATOR: '='; //Single =, rules out: ==,!=,>=,<=
//Confusing name. Generally means a sequence of chars that can be: command, global variable or 
//public variable. More specific checking is done at validation. 
terminal UNRECOGNIZED_STRING: ('a'..'z'|'A'..'Z')('a'..'z'|'A'..'Z'|'0'..'9'|'_')*;

/*
 * Contains automatically generated list of commands without parameters.
 * Checking is difficult to do at validation time as global 
 * variable reference (Requires Xtext cross reference)
 * is syntactically identical with parameterless command
 * 
 * Generating such a list for all commands would mean that lexer
 * static initializer size would be over 64k and hence fail to execute.
 * 
 * CommandLeft and CommandMiddle variants are both checked at the validation
 * time.
 */
 
CommandParameterless:
	name=(
    'accTime'
    |'activatedAddons'
    |'addEditorObject'
    |'addTeamMember'
    |'agent'
    |'agents'
    |'allControls'
    |'allCurators'
    |'allDead'
    |'allDeadMen'
    |'allDisplays'
    |'allGroups'
    |'allMapMarkers'
    |'allMines'
    |'allSites'
    |'allUnits'
    |'allUnitsUAV'
    |'armoryPoints'
    |'ASLToATL'
    |'ATLToASL'
    |'attachedObject'
    |'batteryChargeRTD'
    |'benchmark'
    |'blufor'
    |'briefingName'
    |'buldozer_reloadOperMap'
    |'cadetMode'
    |'cameraOn'
    |'cameraView'
    |'campaignConfigFile'
    |'cancelSimpleTaskDestination'
    |'cbChecked'
    |'cheatsEnabled'
    |'civilian'
    |'className'
    |'clearMagazinePool'
    |'clearOverlay'
    |'clearRadio'
    |'clearWeaponPool'
    |'closeOverlay'
    |'collapseObjectTree'
    |'collectiveRTD'
    |'commandingMenu'
    |'commitOverlay'
    |'configFile'
    |'controlNull'
    |'copyFromClipboard'
    |'createDiaryLink'
    |'ctrlActivate'
    |'ctrlAutoScrollDelay'
    |'ctrlAutoScrollRewind'
    |'ctrlAutoScrollSpeed'
    |'ctrlChecked'
    |'ctrlClassName'
    |'ctrlCommitted'
    |'ctrlDelete'
    |'ctrlFade'
    |'ctrlHTMLLoaded'
    |'ctrlIDC'
    |'ctrlIDD'
    |'ctrlMapAnimClear'
    |'ctrlMapAnimCommit'
    |'ctrlMapAnimDone'
    |'ctrlMapMouseOver'
    |'ctrlMapScale'
    |'ctrlMapWorldToScreen'
    |'ctrlModel'
    |'ctrlModelDirAndUp'
    |'ctrlModelScale'
    |'ctrlParent'
    |'ctrlPosition'
    |'ctrlScale'
    |'ctrlSetFocus'
    |'ctrlSetStructuredText'
    |'ctrlShown'
    |'ctrlTextHeight'
    |'ctrlType'
    |'curatorCamera'
    |'curatorMouseOver'
    |'curatorSelected'
    |'currentTasks'
    |'cursorTarget'
    |'date'
    |'daytime'
    |'debugLog'
    |'deleteTarget'
    |'deleteTeam'
    |'detectedMines'
    |'diag_fps'
    |'diag_fpsmin'
    |'diag_frameno'
    |'diag_tickTime'
    |'dialog'
    |'difficulty'
    |'difficultyEnabledRTD'
    |'disableDebriefingStats'
    |'disableSerialization'
    |'displayNull'
    |'distributionRegion'
    |'drawLocation'
    |'east'
    |'enableEndDialog'
    |'endLoadingScreen'
    |'enemy'
    |'estimatedEndServerTime'
    |'exit'
    |'false'
    |'findEditorObject'
    |'finishMissionInit'
    |'fog'
    |'fogForecast'
    |'fogParams'
    |'forceEnd'
    |'forceWeatherChange'
    |'freeLook'
    |'friendly'
    |'fromEditor'
    |'gearSlotAmmoCount'
    |'gearSlotData'
    |'getArtilleryComputerSettings'
    |'getClientState'
    |'getEditorCamera'
    |'getEditorMode'
    |'getElevationOffset'
    |'getResolution'
    |'getShadowDistance'
    |'getTerrainHeightASL'
    |'getWorld'
    |'groupIconSelectable'
    |'groupIconsVisible'
    |'grpNull'
    |'gusts'
    |'halt'
    |'hasInterface'
    |'hcShownBar'
    |'hierarchyObjectsCount'
    |'HUDMovementLevels'
    |'humidity'
    |'importAllGroups'
    |'importance'
    |'independent'
    |'inheritsFrom'
    |'initAmbientLife'
    |'isAgent'
    |'isArray'
    |'isAutotest'
    |'isDedicated'
    |'isEqualTo'
    |'isInstructorFigureEnabled'
    |'isMultiplayer'
    |'isNumber'
    |'isPiPEnabled'
    |'isRealTime'
    |'isServer'
    |'isShowing3DIcons'
    |'isSteamMission'
    |'isStreamFriendlyUIEnabled'
    |'isText'
    |'isTutHintsEnabled'
    |'language'
    |'lbSelection'
    |'lbSort'
    |'lbSortByValue'
    |'libraryCredits'
    |'libraryDisclaimers'
    |'lightnings'
    |'lineBreak'
    |'lnbGetColumnsPosition'
    |'lnbSize'
    |'loadGame'
    |'loadOverlay'
    |'locationNull'
    |'locationPosition'
    |'logEntities'
    |'mapAnimClear'
    |'mapAnimCommit'
    |'mapAnimDone'
    |'markAsFinishedOnSteam'
    |'members'
    |'missionConfigFile'
    |'missionName'
    |'missionNamespace'
    |'missionStart'
    |'moonIntensity'
    |'musicVolume'
    |'newOverlay'
    |'nextMenuItemIndex'
    |'nextWeatherChange'
    |'nil'
    |'numberOfEnginesRTD'
    |'objNull'
    |'onGroupIconOverEnter'
    |'onGroupIconOverLeave'
    |'openCuratorInterface'
    |'openDSInterface'
    |'opfor'
    |'overcast'
    |'overcastForecast'
    |'parsingNamespace'
    |'particlesQuality'
    |'pi'
    |'playableUnits'
    |'player'
    |'playerRespawnTime'
    |'playerSide'
    |'priority'
    |'processDiaryLink'
    |'processInitCommands'
    |'productVersion'
    |'profileName'
    |'profileNamespace'
    |'progressPosition'
    |'radioVolume'
    |'rain'
    |'rainbow'
    |'rectangular'
    |'registeredTasks'
    |'removeTeamMember'
    |'resetCamShake'
    |'resistance'
    |'resources'
    |'restartEditorCamera'
    |'reversedMouseY'
    |'runInitScript'
    |'safeZoneH'
    |'safeZoneW'
    |'safeZoneWAbs'
    |'safeZoneX'
    |'safeZoneXAbs'
    |'safeZoneY'
    |'saveGame'
    |'saveJoysticks'
    |'saveOverlay'
    |'saveProfileNamespace'
    |'savingEnabled'
    |'scriptDone'
    |'scriptNull'
    |'selectBestPlaces'
    |'selectedEditorObjects'
    |'selectNoPlayer'
    |'sendTask'
    |'serverTime'
    |'setDefaultCamera'
    |'setLeader'
    |'setObjectArguments'
    |'shownArtilleryComputer'
    |'shownChat'
    |'shownCompass'
    |'shownCuratorCompass'
    |'shownGPS'
    |'shownMap'
    |'shownPad'
    |'shownRadio'
    |'shownUAVFeed'
    |'shownWarrant'
    |'shownWatch'
    |'sideEnemy'
    |'sideFriendly'
    |'sideLogic'
    |'sideUnknown'
    |'simulCloudDensity'
    |'simulInClouds'
    |'simulWeatherSync'
    |'size'
    |'slingLoadAssistantShown'
    |'soundVolume'
    |'sunOrMoon'
    |'switchableUnits'
    |'synchronizedTriggers'
    |'systemOfUnits'
    |'taskChildren'
    |'taskCompleted'
    |'taskDescription'
    |'taskDestination'
    |'taskNull'
    |'taskParent'
    |'taskResult'
    |'taskState'
    |'teamMemberNull'
    |'teamName'
    |'teams'
    |'teamSwitch'
    |'teamSwitchEnabled'
    |'teamType'
    |'terminate'
    |'terrainIntersect'
    |'terrainIntersectASL'
    |'textLog'
    |'textLogFormat'
    |'throttleRTD'
    |'throw'
    |'time'
    |'timeMultiplier'
    |'true'
    |'type'
    |'typeName'
    |'uiNamespace'
    |'updateObjectTree'
    |'vehicles'
    |'viewDistance'
    |'visibleCompass'
    |'visibleGPS'
    |'visibleMap'
    |'visibleWatch'
    |'waves'
    |'waypointAttachedObject'
    |'waypointBehaviour'
    |'waypointCombatMode'
    |'waypointDescription'
    |'waypointFormation'
    |'waypointHousePosition'
    |'waypointLoiterRadius'
    |'waypointLoiterType'
    |'waypointName'
    |'waypointScript'
    |'waypointStatements'
    |'waypointTimeout'
    |'waypointType'
    |'west'
    |'wind'
    |'windDir'
    |'windStr'
    |'worldName'
    )
;